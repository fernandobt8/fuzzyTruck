FUNCTION_BLOCK truck

VAR_INPUT
    x : REAL;
    y : REAL;
    angle : REAL;
END_VAR

VAR_OUTPUT
    turn : REAL;
END_VAR

FUZZIFY x
    TERM left := (-0.5,1) (0.38, 0.85) (0.41, 0.12)  (0.5, 0);
    TERM right := (0.5, 0) (0.59, 0.12) (0.62, 0.85) (1.5, 1);
END_FUZZIFY

FUZZIFY y
    TERM down := (-0.5, 1) (0.25, 0.4) (0.75, 0.7) (1, 1);
    TERM dangerDown := (0.7, 0) (0.75, 0.7) (1, 1);
END_FUZZIFY

FUZZIFY angle
	TERM west := (0, 1) (90, 0) (270, 0)(360, 1);
	TERM south := (0, 0) (75, 1) (90, 1) (105, 1) (180, 0);
	TERM east := (90, 0) (180, 1) (270, 0);
	TERM north := (180, 0)(270, 1) (360, 0);
END_FUZZIFY

DEFUZZIFY turn
    TERM turnRight := (-1.5,0) (-1.5,1) (0, 0);
    TERM turnLeft := (0, 0) (1.5,1) (1.5,0);
    TERM reto := (-0.25, 0) (0, 0.2) (0.25, 0);
    METHOD : COG;
    DEFAULT := 0.1;
END_DEFUZZIFY

RULEBLOCK No1
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;
    OR : MAX;
   
    RULE 1 : IF x IS left AND angle IS south 
                THEN turn IS turnLeft;
    
    RULE 2 : IF x IS left AND angle IS north
                THEN turn IS turnRight;

    RULE 3 : IF x IS right AND angle IS north
                THEN turn IS turnLeft;

    RULE 4 : IF x IS right AND angle IS south
                THEN turn IS turnRight;
	
	RULE 5 : IF angle IS south THEN turn IS reto;
	
    RULE 7 : IF y IS down AND angle IS east AND x IS NOT right AND x IS NOT left
                THEN turn IS turnRight;

    RULE 8 : IF y IS down AND angle IS west AND x IS NOT right AND x IS NOT left
                THEN turn IS turnLeft;
     
    RULE 9 : IF y IS dangerDown AND (x IS right OR x IS left OR angle IS NOT south) AND angle IS east 
    			THEN turn IS turnLeft;
   
    RULE 10 : IF y IS dangerDown AND (x IS right OR x IS left OR angle IS NOT south) AND angle IS west 
    			THEN turn IS turnRight;
END_RULEBLOCK

END_FUNCTION_BLOCK